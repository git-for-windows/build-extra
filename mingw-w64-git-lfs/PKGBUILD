# Maintainer: Brendan Forster <brendan@github.com>

_realname="git-lfs"
pkgbase="mingw-w64-${_realname}"
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=3.6.0
pkgrel=1
pkgdesc="An open source Git extension for versioning large files"
install=git-lfs.install
arch=('any')
url="https://github.com/git-lfs/git-lfs"
src_zip_url="${url}/archive/v${pkgver}.zip"
license=('MIT')
groups=('VCS')

case "$CARCH" in
i686)
  zipname="git-lfs-windows-386-v$pkgver.zip"
  folder=git-lfs-$pkgver/
  sha256sum=58b3029f60d51b8775a0cbb21a39b8504967577a8bb4b3feabb1f1a48bf7fb33
  exesuffix=
  ;;
x86_64)
  zipname="git-lfs-windows-amd64-v$pkgver.zip"
  folder=git-lfs-$pkgver/
  sha256sum=62fce4cfd453493966c387db167ba1aa46ecee730ae24a5b902a1d05650fb4ce
  exesuffix=
  ;;
aarch64)
  zipname="git-lfs-windows-arm64-v$pkgver.zip"
  folder=git-lfs-$pkgver/
  sha256sum=6e8d6051760bd90372ed7dfcace02f80dddec374bab61b9525e263722f97de7b
  exesuffix=
  ;;
esac

source=("https://github.com/github/git-lfs/releases/download/v$pkgver/$zipname"
	"$src_zip_url")

# Git LFS' source .zip now contains symbolic links that bsdtar does not like...
noextract=("${src_zip_url##*/}")

sha256sums=("$sha256sum" SKIP)
options=('!strip')

package() {
  install -d -m755 $pkgdir/$MINGW_PREFIX/bin
  install -m755 $srcdir/$folder/git-lfs$exesuffix.exe $pkgdir/$MINGW_PREFIX/bin/git-lfs.exe
  install -d -m755 $pkgdir/$MINGW_PREFIX/share/doc/git-lfs
  install -m755 $srcdir/$folder/README.md $pkgdir/$MINGW_PREFIX/share/doc/git-lfs/README.md
  install -d -m755 $pkgdir/$MINGW_PREFIX/share/doc/git-doc
  for f in $srcdir/$folder/man/html/git-lfs*.1.html
  do
    ff=${f##*/}
    install -m755 $f $pkgdir/$MINGW_PREFIX/share/doc/git-doc/${ff%.1.html}.html
  done
}
