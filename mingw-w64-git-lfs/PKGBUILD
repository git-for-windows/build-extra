# Maintainer: Brendan Forster <brendan@github.com>

_realname="git-lfs"
pkgbase="mingw-w64-${_realname}"
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=2.0.2
pkgrel=2
pkgdesc="An open source Git extension for versioning large files"
install=git-lfs.install
arch=('any')
url="https://github.com/git-lfs/git-lfs"
src_zip_url="${url}/archive/v${pkgver}.zip"
license=('MIT')
groups=('VCS')

case "$CARCH" in
i686)
  folder="git-lfs-windows-386-$pkgver"
  sha256sum=f97f4b107cb46cbdca3be4fb0fc5fd8590ce0364a235101a227b2c0ea153aca3
  ;;
x86_64)
  folder="git-lfs-windows-amd64-$pkgver"
  sha256sum=1b33c8cb6e2e3238b48c639738d22d755a98f7cb62a994d026bb73c158508689
  ;;
esac

source=("https://github.com/github/git-lfs/releases/download/v$pkgver/$folder.zip"
	"$src_zip_url")

# Git LFS' source .zip now contains symbolic links that bsdtar does not like...
noextract=("${src_zip_url##*/}")

sha256sums=("$sha256sum" SKIP)
options=('!strip')

package() {
  install -d -m755 $pkgdir/$MINGW_PREFIX/libexec/git-core
  install -m755 $srcdir/$folder/git-lfs.exe $pkgdir/$MINGW_PREFIX/libexec/git-core/git-lfs.exe
}
