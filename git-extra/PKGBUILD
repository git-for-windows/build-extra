# Maintainer: nalla <nalla@hamal.uberspace.de>

pkgname=('git-extra')
_ver_base=1.1
pkgver=1.1.63.d544d97
pkgrel=1
pkgdesc="Git for Windows extra files"
arch=('i686' 'x86_64')
url="https://github.com/git-for-windows/build-extra"
license=('GPL')
groups=('VCS')
optdepends=('vim' 'filesystem')
install='git-extra.install'
pkgver() {
  printf "%s.%s.%s" "${_ver_base}" "$(git rev-list --count HEAD -- "$startdir")" "$(git rev-parse --short HEAD)"
}

build() {
  test $startdir/$pkgname.install -nt $startdir/$pkgname.install.in ||
  sed -e "/^@@GITCONFIG@@$/r $startdir/gitconfig" -e "/^@@GITCONFIG@@$/d" \
	<$startdir/$pkgname.install.in >$startdir/$pkgname.install

  gcc -o create-shortcut.exe $startdir/create-shortcut.c -luuid -lole32
}

package() {
  case "$CARCH" in
    i686)
      mingwdir="mingw32"
    ;;
    x86_64)
      mingwdir="mingw64"
    ;;
  esac

  install -d -m755 $pkgdir/etc/profile.d
  install -d -m755 $pkgdir/etc/post-install
  install -d -m755 $pkgdir/usr/bin
  install -d -m755 $pkgdir/usr/share/git
  install -d -m755 $pkgdir/$mingwdir/bin
  install -m644 $startdir/inputrc $pkgdir/etc
  install -m644 $startdir/vimrc $pkgdir/etc
  install -m755 $startdir/vi $pkgdir/usr/bin
  install -m755 $startdir/wordpad $pkgdir/usr/bin
  install -m755 create-shortcut.exe $pkgdir/$mingwdir/bin
  install -m755 $startdir/git-prompt.sh $pkgdir/etc/profile.d
  install -m755 $startdir/aliases.sh $pkgdir/etc/profile.d
  install -m755 $startdir/env.sh $pkgdir/etc/profile.d
  install -m755 $startdir/bash_profile.sh $pkgdir/etc/profile.d
  install -m644 $startdir/msys2-32.ico $pkgdir/usr/share/git
  install -m644 $startdir/99-post-install-cleanup.post $pkgdir/etc/post-install
}
