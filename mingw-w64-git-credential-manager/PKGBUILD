# Maintainer: Johannes Schindelin/Matthew J Cheetham

_realname="git-credential-manager"
pkgbase="mingw-w64-${_realname}"
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-git-credential-manager-core")
replaces=("${MINGW_PACKAGE_PREFIX}-git-credential-manager-core")
pkgver=2.7.1
pkgrel=1
_realver=$pkgver
_realtag=v${pkgver}
pkgdesc="Credential Manager for Git"
install=git-credential-manager.install
arch=('any')
project_url="https://github.com/git-ecosystem/git-credential-manager"
license=('MIT')
makedepends=('markdown'
             'unzip')
groups=('VCS')
options=('!strip')

case "$MINGW_PACKAGE_PREFIX" in
  mingw-w64-i686)
    _arch=x86
    _sha=ef0bb67678a8ef20a071375a810bd9ad9a4806c75c1f22d09033daa786dcf446
    ;;
  mingw-w64-x86_64)
    _arch=x64
    _sha=86369d14f0d90e8c5bac4fd829ded47ee1a80a7fdad8cb76ba1938d3d7320f7e
    ;;
  mingw-w64-clang-aarch64)
    _arch=arm64
    _sha=93e5cc57bcb7b5b5fe6614397d99e2b17b017af64c2a8b543453b02a90ee821c
    ;;
  *)
    _arch=x86
    _sha=ef0bb67678a8ef20a071375a810bd9ad9a4806c75c1f22d09033daa786dcf446
    ;;
esac

zip_name="gcm-win-${_arch}-${_realver}.zip"
zip_url="${project_url}/releases/download/${_realtag}/${zip_name}"
src_zip_url="${project_url}/archive/${_realtag}.zip"

source=("${zip_url}" "$src_zip_url")
noextract=("${zip_name}")

sha256sums=("$_sha"
            '67ef666b18f2e0b1c8da9f5b6bdc6c7058de4fdc4918f537e8925a7b5b38f858')

build() {
  markdown "${srcdir}/git-credential-manager-${_realtag#v}/README.md" > "${srcdir}/git-credential-manager-${_realtag#v}/git-credential-manager.html"
}

prepare() {
  mkdir -p "${srcdir}/${_arch}"
  unzip "${srcdir}/${zip_name}" -d "${srcdir}/${_arch}"
}

package() {
  prefix="$pkgdir/${MINGW_PREFIX}"
  bindir="${srcdir}/${_arch}"
  install -d -m755 "${prefix}"/bin
  install -m755 "$bindir"/*.{dll,exe,config} "${prefix}"/bin
  install -d -m755 "${prefix}"/doc/git-credential-manager
  install -m644 "$srcdir"/git-credential-manager-${_realtag#v}/{README.md,LICENSE,NOTICE} "${prefix}"/doc/git-credential-manager
  install -d -m755 "${prefix}"/share/doc/git-doc
  install -m644 "$srcdir"/git-credential-manager-${_realtag#v}/git-credential-manager.html "${prefix}"/share/doc/git-doc
}
