# Maintainer: Johannes Schindelin/Matthew J Cheetham

_realname="git-credential-manager"
pkgbase="mingw-w64-${_realname}"
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-git-credential-manager-core")
replaces=("${MINGW_PACKAGE_PREFIX}-git-credential-manager-core")
pkgver=2.7.2
pkgrel=1
_realver=$pkgver
_realtag=v${pkgver}
pkgdesc="Credential Manager for Git"
install=git-credential-manager.install
arch=('any')
project_url="https://github.com/git-ecosystem/git-credential-manager"
license=('MIT')
makedepends=('markdown'
             'unzip')
groups=('VCS')
options=('!strip')

case "$MINGW_PACKAGE_PREFIX" in
  mingw-w64-i686)
    _arch=x86
    _sha=4010820705b193c010bdee8a661ae66bea6e01a33952ca14bed7485217660fc9
    ;;
  mingw-w64-x86_64)
    _arch=x64
    _sha=6674e105c10a778d9011670d7829182030f8401142357339880d0d4aec622d01
    ;;
  mingw-w64-clang-aarch64)
    _arch=arm64
    _sha=c4cf90ae5d3a1cff10cbc3670df6251daa58ee8c711b056e821347b3f22fbc5a
    ;;
  *)
    _arch=x86
    _sha=4010820705b193c010bdee8a661ae66bea6e01a33952ca14bed7485217660fc9
    ;;
esac

zip_name="gcm-win-${_arch}-${_realver}.zip"
zip_url="${project_url}/releases/download/${_realtag}/${zip_name}"
src_zip_url="${project_url}/archive/${_realtag}.zip"

source=("${zip_url}" "$src_zip_url")
noextract=("${zip_name}")

sha256sums=("$_sha"
            '8e46461c729552ceb154735e86bf343c2d63cc06ca5acc4d93fdfe16538fb731')

build() {
  markdown "${srcdir}/git-credential-manager-${_realtag#v}/README.md" > "${srcdir}/git-credential-manager-${_realtag#v}/git-credential-manager.html"
}

prepare() {
  mkdir -p "${srcdir}/${_arch}"
  unzip "${srcdir}/${zip_name}" -d "${srcdir}/${_arch}"
}

package() {
  prefix="$pkgdir/${MINGW_PREFIX}"
  bindir="${srcdir}/${_arch}"
  install -d -m755 "${prefix}"/bin
  install -m755 "$bindir"/*.{dll,exe,config} "${prefix}"/bin
  install -d -m755 "${prefix}"/doc/git-credential-manager
  install -m644 "$srcdir"/git-credential-manager-${_realtag#v}/{README.md,LICENSE,NOTICE} "${prefix}"/doc/git-credential-manager
  install -d -m755 "${prefix}"/share/doc/git-doc
  install -m644 "$srcdir"/git-credential-manager-${_realtag#v}/git-credential-manager.html "${prefix}"/share/doc/git-doc
}
