# Maintainer: Johannes Schindelin/Matthew J Cheetham

_realname="git-credential-manager"
pkgbase="mingw-w64-${_realname}"
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-git-credential-manager-core")
replaces=("${MINGW_PACKAGE_PREFIX}-git-credential-manager-core")
pkgver=2.7.0
pkgrel=1
_realver=$pkgver
_realtag=v${pkgver}
pkgdesc="Credential Manager for Git"
install=git-credential-manager.install
arch=('any')
project_url="https://github.com/git-ecosystem/git-credential-manager"
license=('MIT')
makedepends=('markdown'
             'unzip')
groups=('VCS')
options=('!strip')

case "$MINGW_PACKAGE_PREFIX" in
  mingw-w64-i686)
    _arch=x86
    _sha=bbed7b0f56517f92825f4a6d2f98cae9757da6455c977f93d24745fceb48b806
    ;;
  mingw-w64-x86_64)
    _arch=x64
    _sha=070c7cf706fbed844757f53d2f9d46ace09745820323264761e4f0bb4f0319bc
    ;;
  mingw-w64-clang-aarch64)
    _arch=arm64
    _sha=2b69098b1d772bf6c3cc70298bfa29d0ecf4b4f61ec86f810fd1121e79f81721
    ;;
  *)
    _arch=x86
    _sha=bbed7b0f56517f92825f4a6d2f98cae9757da6455c977f93d24745fceb48b806
    ;;
esac

zip_name="gcm-win-${_arch}-${_realver}.zip"
zip_url="${project_url}/releases/download/${_realtag}/${zip_name}"
src_zip_url="${project_url}/archive/${_realtag}.zip"

source=("${zip_url}" "$src_zip_url")
noextract=("${zip_name}")

sha256sums=("$_sha"
            'cfd29c2ce47cac3b9264e08884ddd7a67e829616cc984d2f5331ceaa3f55a326')

build() {
  markdown "${srcdir}/git-credential-manager-${_realtag#v}/README.md" > "${srcdir}/git-credential-manager-${_realtag#v}/git-credential-manager.html"
}

prepare() {
  mkdir -p "${srcdir}/${_arch}"
  unzip "${srcdir}/${zip_name}" -d "${srcdir}/${_arch}"
}

package() {
  prefix="$pkgdir/${MINGW_PREFIX}"
  bindir="${srcdir}/${_arch}"
  install -d -m755 "${prefix}"/bin
  install -m755 "$bindir"/*.{dll,exe,config} "${prefix}"/bin
  install -d -m755 "${prefix}"/doc/git-credential-manager
  install -m644 "$srcdir"/git-credential-manager-${_realtag#v}/{README.md,LICENSE,NOTICE} "${prefix}"/doc/git-credential-manager
  install -d -m755 "${prefix}"/share/doc/git-doc
  install -m644 "$srcdir"/git-credential-manager-${_realtag#v}/git-credential-manager.html "${prefix}"/share/doc/git-doc
}
